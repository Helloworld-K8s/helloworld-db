group "com.softeam"

version = rootProject.file("version.properties").text.replace("version=", "").trim()

repositories {
    maven {
        url "https://nexus.k8.wildwidewest.xyz/repository/maven-public/"
        credentials {
            username nexusUsername
            password nexusPassword
        }
    }
}

buildscript {

    ext.spring_boot_version = "2.0.5.RELEASE"

    repositories {

        maven {
            url "https://nexus.k8.wildwidewest.xyz/repository/maven-public/"
            credentials {
                username nexusUsername
                password nexusPassword
            }
        }

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {

        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
        classpath "net.researchgate:gradle-release:2.6.0"
        classpath "gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:5.1.4"
    }
}

apply plugin: "java"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "org.sonarqube"
apply plugin: "maven-publish"
apply plugin: "net.researchgate.release"
apply plugin: "org.flywaydb.flyway"

dependencies {
    compile "org.postgresql:postgresql:42.2.2"
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-jdbc"
    compile "org.flywaydb:flyway-core:4.2.0"
}

publishing {
    publications {
        maven(MavenPublication) {

            from components.java

            repositories {
                maven {

                    if (project.version.endsWith("-SNAPSHOT")) {
                        url "https://nexus.k8.wildwidewest.xyz/repository/maven-snapshots/"
                    } else {
                        url "https://nexus.k8.wildwidewest.xyz/repository/maven-releases/"
                    }

                    credentials {
                        username nexusUsername
                        password nexusPassword
                    }
                }
            }
        }
    }
}

release {
    failOnUnversionedFiles = false
    versionPropertyFile = "version.properties"
}

afterReleaseBuild.dependsOn publish